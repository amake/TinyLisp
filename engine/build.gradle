plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray'
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

group 'org.tinylisp'
version '0.2-SNAPSHOT'

sourceCompatibility = "7"
targetCompatibility = "7"

ext {
    findJavaHome = { version ->
        try {
            "/usr/libexec/java_home -f -v $version".execute().getText().trim()
        } catch (ignored) {
            null
        }
    }
    java7home = System.env.getOrDefault('JAVA7_HOME', findJavaHome("1.7"))
}

tasks.withType(JavaCompile) {
    if (java7home) {
        options.bootstrapClasspath = files("$java7home/jre/lib/rt.jar")
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

task processSource(type: Copy) {
    from sourceSets.main.java
    filter(ReplaceTokens, tokens: [version: version])
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc.outputs
    classifier = 'javadoc'
}

publishing {
    publications {
        myPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId 'engine'
            version version
            artifact sourceJar
            artifact javadocJar
        }
    }
}

bintray {
    user = rootProject.findProperty('bintrayUser')
    key = rootProject.findProperty('bintrayKey')
    publications = ['myPublication']
    pkg {
        repo = 'maven'
        name = 'tinylisp-engine'
    }
}
