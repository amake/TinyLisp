plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray'
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

group 'org.tinylisp'
version '0.6-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7

import org.apache.tools.ant.filters.ReplaceTokens

task processSource(type: Copy) {
    from sourceSets.main.java
    inputs.property 'version', version
    filter(ReplaceTokens, tokens: [version: version])
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc.outputs
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        myPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId 'engine'
            version version
            artifact sourceJar
            artifact javadocJar
        }
    }
}

bintray {
    user = rootProject.findProperty('bintrayUser')
    key = rootProject.findProperty('bintrayKey')
    publications = ['myPublication']
    pkg {
        repo = 'maven'
        name = 'tinylisp-engine'
    }
}
