plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray'
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

group 'org.tinylisp'
version '0.1-SNAPSHOT'

sourceCompatibility = "7"
targetCompatibility = "7"

ext {
    findJavaHome = { version ->
        try {
            "/usr/libexec/java_home -f -v $version".execute().getText().trim()
        } catch (ignored) {
            null
        }
    }
    java7home = System.env.getOrDefault('JAVA7_HOME', findJavaHome("1.7"))
}

tasks.withType(JavaCompile) {
    if (java7home) {
        options.bootstrapClasspath = files("$java7home/jre/lib/rt.jar")
    }
}

jar {
    manifest {
        attributes 'Implementation-Version': version
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    from javadoc.outputs
}

publishing {
    publications {
        myPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId 'engine'
            version version
            artifact sourceJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}

bintray {
    user = rootProject.findProperty('bintrayUser')
    key = rootProject.findProperty('bintrayKey')
    publications = ['myPublication']
    pkg {
        repo = 'maven'
        name = 'tinylisp-engine'
    }
}
