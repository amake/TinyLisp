// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath 'com.android.tools.build:gradle:4.1.3'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    findJavaHome = { version ->
        try {
            "/usr/libexec/java_home -f -v $version".execute().getText().trim()
        } catch (ignored) {
            null
        }
    }
    java7home = System.env.getOrDefault('JAVA7_HOME', findJavaHome("1.7"))

    def propsFile = file('local.properties')
    if (propsFile.file) {
        def props = new Properties()
        propsFile.withInputStream { props.load(it) }
        props.each { k, v -> set(k, v) }
    } else {
        logger.log(LogLevel.WARN, "$propsFile not found")
    }
}

gradle.projectsEvaluated {
    subprojects {
        if (findProperty("sourceCompatibility") == JavaVersion.VERSION_1_7) {
            tasks.withType(JavaCompile) {
                if (java7home) {
                    options.bootstrapClasspath = files("$java7home/jre/lib/rt.jar")
                }
            }
        }
        if (findProperty('publishing')) {
            publishing {
                publications.mavenJava {
                    pom {
                        name = 'TinyLisp'
                        description = 'A simple Lisp implementation for Java'
                        url = 'http://tinylisp.org'
                        scm {
                            connection = 'scm:git:https://github.com/amake/TinyLisp'
                            developerConnection = 'scm:git:https://github.com/amake/TinyLisp'
                            url = 'https://github.com/amake/TinyLisp.git'
                        }
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'amake'
                                name = 'Aaron Madlon-Kay'
                                email = 'aaron@madlon-kay.com'
                            }
                        }
                    }
                }
                repositories {
                    maven {
                        url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                        credentials(PasswordCredentials) {
                            username rootProject.findProperty('ossrhUsername')
                            password rootProject.findProperty('ossrhPassword')
                        }
                    }
                }
            }
            signing {
                sign publishing.publications.mavenJava
            }
        }
    }
}

task allDependencies {
    dependsOn allprojects.collect { "$it.path:dependencies" }
}
